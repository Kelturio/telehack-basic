1 MAXSTACK = 0
2 DIM STACK$(1000)
10 GOTO 10000
100 REM PUSH STACK
110 IF MAXSTACK > 1000 THEN PRINT "Stack full" : RETURN
120 MAXSTACK = MAXSTACK + 1
130 STACK$(MAXSTACK) = VALUE$
140 RETURN
300 REM POP STACK
310 IF MAXSTACK < 1 THEN PRINT "Stack empty" : RETURN
315 RET$ = STACK$(MAXSTACK)
320 STACK$(MAXSTACK) = ""
330 MAXSTACK = MAXSTACK - 1
340 RETURN
400 REM Split
410 FOR I = 1 TO LEN(INTXT$)
420 IF MID$(INTXT$, I, 1) = " " THEN GOTO 500
430 TSTR$ = TSTR$ + MID$(INTXT$ ,I, 1)
440 NEXT I
450 RETURN
500 REM Split Space found
510 VALUE$ = TSTR$
520 IF VAL(VALUE$) > 0 THEN GOSUB 100
525 TSTR$ = ""
530 IF VALUE$ = "+" THEN GOSUB 1000
540 IF VALUE$ = "-" THEN GOSUB 1500
550 IF VALUE$ = "*" THEN GOSUB 2000
560 IF VALUE$ = "/" THEN GOSUB 2500
570 IF VALUE$ = "dup" THEN GOSUB 3000
580 IF VALUE$ = "swap" THEN GOSUB 3500
590 IF VALUE$ = "drop" THEN GOSUB 4000
600 IF VALUE$ = "rot" THEN GOSUB 4500
610 IF VALUE$ = "over" THEN GOSUB 5000
620 IF VALUE$ = "." THEN GOSUB 5500
630 IF VALUE$ = ".s" THEN GOSUB 6000
640 GOTO 440
1000 REM addition
1010 IF MAXSTACK < 2 THEN PRINT "Not enough Items on Stack" : RETURN
1020 GOSUB 300
1030 LEFTITEM% = VAL(RET$)
1040 GOSUB 300
1050 RIGHTITEM% = VAL(RET$)
1060 VALUE$ = STR$(LEFTITEM% + RIGHTITEM%)
1070 GOSUB 100
1080 RETURN
1500 REM subtraction
1510 IF MAXSTACK < 2 THEN PRINT "Not enough Items on Stack" : RETURN
1520 GOSUB 300
1530 LEFTITEM% = VAL(RET$)
1540 GOSUB 300
1550 RIGHTITEM% = VAL(RET$)
1560 VALUE$ = STR$(RIGHTITEM% - LEFTITEM%)
1570 GOSUB 100
1580 RETURN
2000 REM Multiplication
2010 IF MAXSTACK < 2 THEN PRINT "Not enough Items on Stack" : RETURN
2020 GOSUB 300
2030 LEFTITEM% = VAL(RET$)
2040 GOSUB 300
2050 RIGHTITEM% = VAL(RET$)
2060 VALUE$ = STR$(LEFTITEM% * RIGHTITEM%)
2070 GOSUB 100
2080 RETURN
2500 REM Division
2510 IF MAXSTACK < 2 THEN PRINT "Not enough Items on Stack" : RETURN
2520 GOSUB 300
2530 LEFTITEM% = VAL(RET$)
2540 GOSUB 300
2550 RIGHTITEM% = VAL(RET$)
2560 VALUE$ = STR$(RIGHTITEM% / LEFTITEM%)
2570 GOSUB 100
2580 RETURN
3000 REM dup
3010 IF MAXSTACK < 1 THEN PRINT "Not enough Items on Stack" : RETURN
3020 GOSUB 300
3030 VALUE$ = RET$
3040 GOSUB 100
3050 GOSUB 100
3060 RETURN
3500 REM swap
3510 IF MAXSTACK < 2 THEN PRINT "Not enough Items on Stack" : RETURN
3520 GOSUB 300
3530 LEFTITEM% = VAL(RET$)
3540 GOSUB 300
3550 RIGHTITEM% = VAL(RET$)
3560 VALUE$ = STR$(LEFTITEM%)
3570 GOSUB 100
3580 VALUE$ = STR$(RIGHTITEM%)
3590 GOSUB 100
3600 RETURN
4000 REM drop
4010 IF MAXSTACK < 1 THEN PRINT "Not enough Items on Stack" : RETURN
4020 GOSUB 300
4030 RETURN
4500 REM rot
4510 IF MAXSTACK < 3 THEN PRINT "Not enough Items on Stack" : RETURN
4520 GOSUB 300
4530 LEFTITEM% = VAL(RET$)
4540 GOSUB 300
4550 MIDITEM% = VAL(RET$)
4560 GOSUB 300
4570 RIGHTITEM% = VAL(RET$)
4580 VALUE$ = STR$(MIDITEM%)
4590 GOSUB 100
4600 VALUE$ = STR$(LEFTITEM%)
4610 GOSUB 100
4620 VALUE$ = STR$(RIGHTITEM%)
4630 GOSUB 100
4640 RETURN
5000 REM over
5010 IF MAXSTACK < 2 THEN PRINT "Not enough Items on Stack" : RETURN
5020 GOSUB 300
5030 LEFTITEM% = VAL(RET$)
5040 GOSUB 300
5050 RIGHTITEM% = VAL(RET$)
5060 VALUE$ = STR$(RIGHTITEM%)
5070 GOSUB 100
5080 VALUE$ = STR$(LEFTITEM%)
5090 GOSUB 100
5100 VALUE$ = STR$(RIGHTITEM%)
5110 GOSUB 100
5120 RETURN
5500 REM .
5510 IF MAXSTACK < 1 THEN PRINT "Not enough Items on Stack" : RETURN
5520 GOSUB 300
5530 PRINT RET$
5540 RETURN
6000 REM .s
6010 FOR II = 1 TO MAXSTACK
6020 PRINT STACK$(II)
6030 NEXT II
6040 RETURN
10000 REM Start
10010 INPUT "FORTH>"; INTXT$
10015 INTXT$ = INTXT$ + " "
10020 GOSUB 410
10030 GOTO 10000
